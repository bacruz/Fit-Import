/********************************************************** {{{1 ***********
 *  Copyright © 2015 "Martin Krischik" «krischik@users.sourceforge.net»
 ***************************************************************************
 *  This program is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  This program is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with this program.  If not, see http://www.gnu.org/licenses/
 ********************************************************** }}}1 **********/

group = "com.krischik"
version = "1.0.0"
description = "Import KetFit and Withings exports into Google Fit — Android App"

buildscript {
   repositories {
      mavenCentral ()
      mavenCentral ()
   }
   dependencies {
      classpath (
         [group: 'com.android.tools.build', name: 'gradle', version: '1.1.0'],
         [group: 'com.neenbedankt.gradle.plugins', name: 'android-apt', version: '1.4'],
         [group: 'com.jakewharton.hugo', name: 'hugo-plugin', version: '1.2.1'],
         [group: 'org.jetbrains.kotlin', name: 'kotlin-gradle-plugin', version: kotlin_version]
      )
   }
}

apply plugin: 'com.android.application'
apply plugin: 'com.jakewharton.hugo'
apply plugin: 'android-apt'
apply plugin: 'kotlin-android'

repositories {
   mavenCentral ()
   mavenLocal ()
   mavenCentral ()
}

android {
   compileSdkVersion 21
   buildToolsVersion "21.1.2"

   applicationVariants.all { variant ->
      variant.outputs.each { output ->
         def name = output.outputFile.name
            .replace (".apk", "-${variant.versionName}.apk")
            .replace (project.name, "Fit-Import")

         output.outputFile = new File (output.outputFile.parent, name)
      }
   }

   signingConfigs {
      debug {
         storeFile file (System.getenv ("KEY_STORE"))
         storePassword System.getenv ("KEY_STOREPASS")
         keyAlias System.getenv ("KEY_ALIAS")
         keyPassword System.getenv ("KEY_KEYPASS")
      }
      release {
         storeFile file (System.getenv ("KEY_STORE"))
         storePassword System.getenv ("KEY_STOREPASS")
         keyAlias System.getenv ("KEY_ALIAS")
         keyPassword System.getenv ("KEY_KEYPASS")
      }
   }

   defaultConfig {
      applicationId "com.krischik.fit_import"
      minSdkVersion 15
      targetSdkVersion 21
      versionCode 10000
      versionName "1.0.0"
   }
   buildTypes {
      release {
         signingConfig signingConfigs.release
         minifyEnabled false
         proguardFiles getDefaultProguardFile ('proguard-android.txt'), 'proguard-rules.pro'
      }
      debug {
         signingConfig signingConfigs.debug
      }
   }
   sourceSets {
      main.java.srcDirs += 'src/main/kotlin'
      androidTest.java.srcDirs += 'src/androidTest/kotlin'
   }
   compileOptions {
      sourceCompatibility JavaVersion.VERSION_1_7
      targetCompatibility JavaVersion.VERSION_1_7
   }
}

dependencies {
   apt (
      [group: 'org.androidannotations', name: 'androidannotations', version: androidannotations_version]
   )
   compile (
      project (':lib'),
      [group: 'com.android.support', name: 'appcompat-v7', version: androidsupport_version],
      [group: 'com.android.support', name: 'support-v4', version: androidsupport_version],
      [group: 'com.google.android.gms', name: 'play-services', version: '6.1.+'],
      [group: 'org.androidannotations', name: 'androidannotations-api', version: androidannotations_version],
      [group: 'org.jetbrains.kotlin', name: 'kotlin-stdlib', version: kotlin_version]
   )
   androidTestCompile (
      [group: 'com.jayway.android.robotium', name: 'robotium-solo', version: '5.3.1'],
      [group: 'com.github.rtyley', name: 'android-screenshot-celebrity', version: '1.9']
   )
}

apt {
   arguments {
      androidManifestFile variant.outputs [0].processResources.manifestFile
      // if you have multiple outputs (when using splits), you may want to have other index than 0

      resourcePackageName 'com.krischik.fit_import'

      // If you're using Android NBS flavors you should use the following line instead of hard-coded packageName
      // resourcePackageName android.defaultConfig.packageName

      // You can set optional annotation processing options here, like these commented options:
      // logLevel 'INFO'
      // logFile '/var/log/aa.log'
   }
}
// vim: set nowrap tabstop=8 shiftwidth=4 softtabstop=4 expandtab :
// vim: set textwidth=0 filetype=groovy foldmethod=marker nospell :
